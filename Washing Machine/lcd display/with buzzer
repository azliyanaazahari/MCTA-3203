#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <esp_now.h>
#include <WiFi.h>

// Define pins
#define BUTTON_PIN 25
#define POTENTIOMETER_PIN 34
#define BUZZER_PIN 26 // Buzzer pin

// LCD Initialization for 20x4 screen
LiquidCrystal_I2C lcd(0x27, 20, 4);

// OLED Initialization
#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 32
#define OLED_RESET -1
Adafruit_SSD1306 oled(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);

// Define ESP-NOW settings
uint8_t receiverAddress[] = {0xC0, 0x5D, 0x89, 0xDC, 0xA5, 0x00};

// Data structure for transmission
typedef struct {
  char temperature[8];
  char time[8];
  char function[8];
  int timeRemaining;
} UserData;

UserData userData;

// Variables for selection
const char *temperatureOptions[] = {"20C", "40C", "60C", "90C"};
const char *timeOptions[] = {"1m", "30m", "45m", "60m"};
const char *functionOptions[] = {"Wash", "Rinse", "Spin"};

int step = 0;
int selectedIndices[] = {0, 0, 0};
unsigned long lastDebounceTime = 0;
const unsigned long debounceDelay = 50;

bool buttonState = HIGH;
bool lastButtonState = HIGH;

// Function to parse time string
int parseTime(const char *timeStr) {
  int timeValue = atoi(timeStr);
  char unit = timeStr[strlen(timeStr) - 1];

  if (unit == 'm') {
    return timeValue * 60;
  }
  return 0;
}

// Callback for sending data
void onSent(const uint8_t *macAddr, esp_now_send_status_t status) {
  Serial.println(status == ESP_NOW_SEND_SUCCESS ? "Send Success" : "Send Fail");
}

void setup() {
  lcd.init();
  lcd.backlight();

  if (!oled.begin(SSD1306_SWITCHCAPVCC, 0x3C)) {
    Serial.println(F("OLED initialization failed!"));
    while (1);
  }
  oled.display();

  oled.setTextSize(2);
  oled.setTextColor(WHITE);

  pinMode(BUTTON_PIN, INPUT_PULLUP);
  pinMode(POTENTIOMETER_PIN, INPUT);
  pinMode(BUZZER_PIN, OUTPUT); // Initialize buzzer pin

  WiFi.mode(WIFI_STA);
  if (esp_now_init() != ESP_OK) {
    Serial.println("ESP-NOW Initialization Failed");
    return;
  }
  esp_now_register_send_cb(onSent);

  esp_now_peer_info_t peerInfo = {};
  memcpy(peerInfo.peer_addr, receiverAddress, 6);
  peerInfo.channel = 0;
  peerInfo.encrypt = false;
  esp_now_add_peer(&peerInfo);

  lcd.setCursor(0, 0);
  String message = "Select Temp:";
  int messageLength = message.length();
  int centerPosition = (20 - messageLength) / 2;
  lcd.setCursor(centerPosition, 0);
  lcd.print(message);
  lcd.setCursor(0, 1);
  Serial.begin(115200);
}

void loop() {
  int reading = digitalRead(BUTTON_PIN);
  if (reading != lastButtonState) {
    lastDebounceTime = millis();
  }
  if ((millis() - lastDebounceTime) > debounceDelay) {
    if (reading == LOW && buttonState == HIGH) {
      buttonPressed();
    }
    buttonState = reading;
  }
  lastButtonState = reading;

  if (step < 3) {
    int potValue = analogRead(POTENTIOMETER_PIN);
    int maxIndex = step == 0 ? 3 : step == 1 ? 3 : 2;
    selectedIndices[step] = constrain(map(potValue, 0, 4095, 0, maxIndex), 0, maxIndex);

    lcd.setCursor(0, 1);
    lcd.print("                "); // Clear line
    lcd.setCursor(0, 1);
    displayOptionsOnOLED();
  }
}

void buttonPressed() {
  step++;
  lcd.clear();
  displayOptionsOnOLED();

  if (step == 1) {
    lcd.setCursor(0, 0);
    lcd.print("Select Time:");
  } else if (step == 2) {
    lcd.setCursor(0, 0);
    lcd.print("Select Func:");
  } else if (step == 3) {
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("Summary:");
    lcd.setCursor(0, 2);
    lcd.print(temperatureOptions[selectedIndices[0]]);
    lcd.print(", ");
    lcd.print(timeOptions[selectedIndices[1]]);
    lcd.print(", ");
    lcd.print(functionOptions[selectedIndices[2]]);

    delay(500);
    displayWaitMessageOnOLED();
    delay(2500);

    strcpy(userData.temperature, temperatureOptions[selectedIndices[0]]);
    strcpy(userData.time, timeOptions[selectedIndices[1]]);
    strcpy(userData.function, functionOptions[selectedIndices[2]]);
    userData.timeRemaining = parseTime(userData.time);

    esp_now_send(receiverAddress, (uint8_t *)&userData, sizeof(userData));
    startCountdown(userData.timeRemaining);
    resetSystem();
  }
}

void displayWaitMessageOnOLED() {
  oled.clearDisplay();
  oled.setTextSize(3);
  oled.setTextColor(WHITE);
  oled.setCursor(20, 10);
  oled.print("Wait...");
  oled.display();
}

void startCountdown(int totalSeconds) {
  while (totalSeconds >= 0) {
    oled.clearDisplay();
    oled.setCursor(0, 10);
    oled.print(totalSeconds / 60);
    oled.print("m ");
    oled.print(totalSeconds % 60);
    oled.print("s");
    oled.display();
    delay(1000);
    totalSeconds--;

    userData.timeRemaining = totalSeconds;
    esp_now_send(receiverAddress, (uint8_t *)&userData, sizeof(userData));
  }

  oled.clearDisplay();
  oled.setCursor(0, 0);
  oled.print("Complete!");
  oled.display();

  // Buzzer for active type
  for (int i = 0; i < 5; i++) {
    digitalWrite(BUZZER_PIN, HIGH);
    delay(500);
    digitalWrite(BUZZER_PIN, LOW);
    delay(500);
  }

  delay(3000);
}

void resetSystem() {
  step = 0;
  memset(selectedIndices, 0, sizeof(selectedIndices));

  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Select Temp:");
}

void displayOptionsOnOLED() {
  oled.clearDisplay();
  oled.setTextSize(3);
  oled.setTextColor(WHITE);

  String message = (step == 0) ? temperatureOptions[selectedIndices[0]]
                                : (step == 1) ? timeOptions[selectedIndices[1]]
                                              : functionOptions[selectedIndices[2]];

  oled.setCursor(20, 10);
  oled.print(message);
  oled.display();
}
